?start: sequent

?sequent: antecedent TURNSTILE succedent
        | TURNSTILE succedent
?antecedent: formulas
?succedent: formulas

formulas: formula ("," formula)*
formula: atom
        | "(" formula ")"
        | NEGATION formula -> negation
        | formula CONJUNCTION formula -> conjunction
        | formula DISJUNCTION formula -> disjunction
        | formula IMPLICATION formula -> implication
        | formula IFF formula -> iff

atom: GREEK | NAME | CAPITALIZED_NAME
CAPITALIZED_NAME: /[A-Z][a-z]+/
GREEK: /[αβγδεζηθικλμνξοπρστυφχψωΑΒΓΔΕΖΗΘΙΚΛΜΝΞΟΠΡΣΤΥΦΧΨΩ]+/
NEGATION: /~|¬/
CONJUNCTION: /&|∧/
DISJUNCTION: /\||∨/
IMPLICATION: /=>|⇒/
IFF: /<=>|⇔/

TURNSTILE: /⊢|\|-/

%import common.CNAME -> NAME
%import common.WS
%ignore WS